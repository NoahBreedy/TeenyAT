;--------
;  It may not be proper for me to have the preproccessor grammar here
;  since our bottom level parser dosent actually recieve these tokens
;--------

<program> ::= <line>* T_EOL

<line>    ::= <pp_directive>
            | <statement>
            | T_EOL

<statement> ::= <statement_body> T_EOL

<statement_body> ::= <label_line>
                   | <constant_line>
                   | <variable_line>
                   | <raw_line>
                   | <code_line>

<pp_directive> ::= T_AT_DIRECTIVE <pp_identifier> T_EOL

<pp_arg>   ::= T_STRING
             | T_NUMBER
             | T_LABEL
             | T_IDENTIFIER

;--------
; I dont pass these in as tokens since its simpler
; to handle this as terminal strings
;--------
<pp_identifier> ::= "ifdef"    T_IDENTIFIER
                  | "ifndef"   T_IDENTIFIER
                  | "endif"
                  | "define"   T_IDENTIFIER <pp_arg>?
                  | "undefine" T_IDENTIFIER
                  | "include"  T_STRING

<label_line> ::= T_LABEL

<raw_value> ::=  T_LABEL
              |  T_NUMBER
              |  T_CHARACTER
              |  T_IDENTIFIER

<immediate> ::=  <plus_or_minus>? <raw_value>
<no_sign_immediate> ::= <raw_value>


<plus_or_minus> ::= T_PLUS
                  | T_MINUS

<register_and_immediate> ::= T_REGISTER <includes_immediate>?
                           | <immediate> <includes_register>?

<includes_immediate> ::= <plus_or_minus> <no_sign_immediate>

<includes_register> ::= T_PLUS T_REGISTER

<constant_line> ::= T_CONSTANT T_IDENTIFIER <immediate>

<variable_line> ::= T_VARIABLE T_IDENTIFIER <variable_format>

<variable_format> ::= T_LBRACKET <no_sign_immediate> T_RBRACKET
                    | T_STRING
                    | T_PACKED_STRING
                    | <immediate>*

<raw_line> ::= T_RAW <raw_line_value>*

<raw_line_value> ::= <immediate>
                   | T_STRING
                   | T_PACKED_STRING

<code_line> ::= <set_instruction>
              | <lod_instruction>
              | <str_instruction>
              | <psh_instruction>
              | <pop_instruction>
              | <bts_instruction>
              | <btc_instruction>
              | <btf_instruction>
              | <cal_instruction>
              | <add_instruction>
              | <sub_instruction>
              | <mpy_instruction>
              | <div_instruction>
              | <mod_instruction>
              | <and_instruction>
              | <or_instruction>
              | <xor_instruction>
              | <shf_instruction>
              | <shl_instruction>
              | <rot_instruction>
              | <rol_instruction>
              | <ror_instruction>
              | <neg_instruction>
              | <cmp_instruction>
              | <dly_instruction>
              | <int_instruction>
              | <rti_instruction>
              | <jmp_instruction>
              | <je_instruction>
              | <jne_instruction>
              | <jl_instruction>
              | <jle_instruction>
              | <jg_instruction>
              | <jge_instruction>
              | <lup_instruction>
              | <inc_instruction>
              | <dec_instruction>
              | <inv_instruction>
              | <ret_instruction>

<set_instruction> ::= T_SET T_REGISTER T_COMMA <register_and_immediate>

<lod_instruction> ::= T_LOD T_REGISTER T_COMMA T_LBRACKET <register_and_immediate> T_RBRACKET

<str_instruction> ::= T_STR T_LBRACKET <register_and_immediate> T_RBRACKET T_COMMA T_REGISTER

<psh_instruction> ::= T_PSH <psh_format>
<psh_format>      ::= T_LBRACKET T_REGISTER T_RBRACKET T_COMMA <register_and_immediate>
                    | <register_and_immediate>

<pop_instruction> ::= T_POP T_REGISTER <pop_format>?
<pop_format>      ::= T_COMMA T_LBRACKET T_REGISTER T_RBRACKET

<bts_instruction> ::= T_BTS T_REGISTER T_COMMA <register_and_immediate>
<btc_instruction> ::= T_BTC T_REGISTER T_COMMA <register_and_immediate>
<btf_instruction> ::= T_BTF T_REGISTER T_COMMA <register_and_immediate>

; uses reg2
<cal_instruction> ::= T_CAL <register_and_immediate>

<add_instruction> ::= T_ADD T_REGISTER T_COMMA <register_and_immediate>
<sub_instruction> ::= T_SUB T_REGISTER T_COMMA <register_and_immediate>
<mpy_instruction> ::= T_MPY T_REGISTER T_COMMA <register_and_immediate>
<div_instruction> ::= T_DIV T_REGISTER T_COMMA <register_and_immediate>
<mod_instruction> ::= T_MOD T_REGISTER T_COMMA <register_and_immediate>
<and_instruction> ::= T_AND T_REGISTER T_COMMA <register_and_immediate>
<or_instruction>  ::= T_OR  T_REGISTER T_COMMA <register_and_immediate>
<xor_instruction> ::= T_XOR T_REGISTER T_COMMA <register_and_immediate>

<shf_instruction> ::= T_SHF T_REGISTER T_COMMA <register_and_immediate>
<shl_instruction> ::= T_SHL T_REGISTER T_COMMA <immediate>
<shr_instruction> ::= T_SHR T_REGISTER T_COMMA <immediate>

<rot_instruction> ::= T_ROT T_REGISTER T_COMMA <register_and_immediate>
<rol_instruction> ::= T_ROL T_REGISTER T_COMMA <immediate>
<ror_instruction> ::= T_ROR T_REGISTER T_COMMA <immediate>

<neg_instruction> ::= T_NEG T_REGISTER

<cmp_instruction> ::= T_CMP T_REGISTER T_COMMA <register_and_immediate>

<dly_instruction>       ::= T_DLY <dly_format>
<dly_format>            ::= T_REGISTER <dly_register_format>?
                          | <immediate> <includes_register>?
<dly_register_format>   ::= T_COMMA <register_and_immediate>
                          | <immediate>

<int_instruction> ::= T_INT <register_and_immediate>

<rti_instruction> ::= T_RTI

<jmp_instruction> ::= T_JMP <register_and_immediate>
<je_instruction>  ::= T_JE  <register_and_immediate>
<jne_instruction> ::= T_JNE <register_and_immediate>
<jl_instruction>  ::= T_JL  <register_and_immediate>
<jle_instruction> ::= T_JLE <register_and_immediate>
<jg_instruction>  ::= T_JG  <register_and_immediate>
<jge_instruction> ::= T_JGE <register_and_immediate>

<lup_instruction> ::= T_LUP T_REGISTER T_COMMA <register_and_immediate>

<inc_instruction> ::= T_INC T_REGISTER

<dec_instruction> ::= T_INC T_REGISTER

<inv_instruction> ::= T_NEG T_REGISTER

<ret_instruction> ::= T_RET
